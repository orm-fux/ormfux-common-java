apply plugin: 'eclipse'

configure(subprojects) {
	apply plugin: 'eclipse'
	apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	
	/* Java version */
	sourceCompatibility = '11'
	targetCompatibility = '11'
	
	/* Identifier */
	group = 'org.ormfux'
	archivesBaseName = project.name
	version = '1.4'
	
	/* dependencies */
	dependencies {
		implementation 'org.apache.commons:commons-lang3:3.7'
		testImplementation 'junit:junit:4.11'
	}
	
	/* unit test resources folder */
	task copyTestResources(type: Copy) {
	    from "${projectDir}/src/test/resources"
	    into "${buildDir}/classes/java/test"
	}
	
	processTestResources.dependsOn copyTestResources
		
	/* repos */
	repositories {
		mavenCentral()
		mavenLocal()
	}
	
	/* Maven artifact creation */
	task javadocJar(type: Jar) {
		from javadoc
		classifier = 'javadoc'
	}
	
	task sourcesJar(type: Jar) {
		from sourceSets.main.allJava
		classifier = 'sources'
	}
	
	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
	
				artifact sourcesJar
				artifact javadocJar
	
				afterEvaluate {
					artifactId = project.name
				}
			}
		}
	}
	
	if (JavaVersion.current().isJava8Compatible()) {
		allprojects {
			tasks.withType(Javadoc) {
				options.addStringOption('Xdoclint:none', '-quiet')
			}
		}
	}
	
}
